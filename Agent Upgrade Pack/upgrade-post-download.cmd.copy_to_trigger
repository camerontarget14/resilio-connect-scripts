rem ---------------- Script updated on 2024-09-17 --------------------------
rem Uncomment the line below to prevent script from checking free disk space required for upgrade
rem set DontCheckDiskSpace="-NoDiskSpaceCheck"

rem Uncomment the line below to skip upgrading (installing) file locking driver
rem set DontUpgradeFLDriver="-NoFLDriverUpgrade"

powershell.exe -NoProfile -Command "$PSVersionTable.PSVersion.Major" > ps.version
set /p PSVER=<ps.version

if %PSVER% LSS 4 (
echo Powershell version is older than 4.0, automatic upgrade not possible
exit /b 17
)

if not exist "agent_upgrade.ps1" (
echo agent_upgrade.ps1 script not found
exit /b 4
)

findstr /I "HTML" agent_upgrade.ps1 > NULL

if %ERRORLEVEL%==0 (
echo agent_upgrade.ps1 script is actually an HTML page, please re-download from github
exit /b 18
)

powershell.exe -NoProfile -ExecutionPolicy Bypass -File agent_upgrade.ps1 -Verify -NoX86exeCheck %DontCheckDiskSpace% %DontUpgradeFLDriver% > verify.log 2>&1

set ERR=%ERRORLEVEL%

type verify.log

if %ERR%==1 (
echo No need to upgrade, same version detected
exit /b 0
)

if %ERR% GEQ 2 (
exit /b %ERR%
)

echo %APPDATA%\Resilio Connect Agent Service>oldstorage.path

powershell.exe -NoProfile -ExecutionPolicy Bypass -File agent_upgrade.ps1 -CreateUpgradeTask -NoExtensionUpgrade %DontUpgradeFLDriver%
powershell.exe -NoProfile -ExecutionPolicy Bypass -File agent_upgrade.ps1 -RunUpgradeTask
 
